$$include '../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Derani-Shared : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes
	define u XH
	define ada ArchDepthA
	define adb ArchDepthB
	define l SB
	define r RightSB
	define middle : 0.5 * (l + r)

	create-glyph 'saqseoq' 0xF16BA : glyph-proc
		include : MarkSet.e
		local d 0
		include : dispiro
			widths.lhs Stroke
			g4 middle (u - O) [heading Leftward]
			archv
			flat (l + OX) (u - ada)
			curl (l + OX) (d + adb)
			arcvh
			curl middle (d + O)
			archv
			flat (r - OX) (d + ada)
			curl (r - OX) (u - adb)
			arcvh
			close

	create-glyph 'cecoa' 0xF16B9 : glyph-proc
		include : MarkSet.e
		local d 0
		include : dispiro
			widths.lhs Stroke
			g4 [mix l r 0.6] (u - O) [heading Leftward]
			curl [mix l r 0.5] (u - O) [heading Leftward]
			archv
			flat (l + OX) (u - ada)
			curl (l + OX) (d + adb)
			arcvh
			curl middle (d + O)
			archv
			flat (r - OX) (d + ada) [heading Upward]

	create-glyph 'bubue' 0xF16B2 : glyph-proc
		include : MarkSet.e
		local d 0
		include : dispiro
			widths.rhs Stroke
			g4 (l + OX) (u - Hook) [heading Upward]
			arcvh
			curl middle (u - O)
			archv
			flat (r - OX) (u - ada)
			curl (r - OX) (d + adb)
			arcvh
			flat middle (d + O) [heading Leftward]
			curl [mix l r 0.4] (d + O) [heading Leftward]

	derive-glyphs 'kikue' 0xF16C4 "cecoa" : function [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include [refer-glyph 'dotBelow']

	create-glyph 'rairua' 0xF16BB : glyph-proc
		local a ada
		local sw Stroke
		define fine ShoulderFine
		include : MarkSet.e
		include : HBar.b l r 0
		include : VBar.l l 0 u
		include : union
			dispiro
				flat (l + [HSwToV sw] - [HSwToV fine]) (u - a - 2) [widths.rhs fine]
				curl (l + [HSwToV sw] - [HSwToV fine]) (u - a)
				arch.rhs u (sw -- sw) (swBefore -- fine)
				flat r (u - adb) [widths.rhs sw]
				curl r Descender

		if SLAB : begin
			include : HSerif.lt l u SideJut
			include : HSerif.lb l 0 SideJut
			include : HSerif.lb (r - [HSwToV HalfStroke]) Descender MidJutSide
			include : HSerif.rb (r - [HSwToV HalfStroke]) Descender Jut

	create-glyph 'mamei_coda' 0xF16B1 : glyph-proc
		local a ada
		local sw Stroke
		define fine ShoulderFine
		include : MarkSet.e
		include : HBar.b l r 0
		include : VBar.l l 0 u
		include : union
			dispiro
				flat (l + [HSwToV sw] - [HSwToV fine]) (u - a - 2) [widths.rhs fine]
				curl (l + [HSwToV sw] - [HSwToV fine]) (u - a)
				arch.rhs u (sw -- sw) (swBefore -- fine)
				flat r (u - adb) [widths.rhs sw]
				curl r 0

		if SLAB : begin
			include : HSerif.lt l u SideJut
			include : HSerif.lb l 0 SideJut
			include : HSerif.rb (r - [HSwToV HalfStroke]) 0 Jut

	create-glyph 'mamei' 0xF16B0 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		local l df.leftSB
		local r df.rightSB
		local a ada
		local sw df.mvs
		define fine ShoulderFine
		include : MarkSet.e
		include : HBar.b l r 0
		include : VBar.l l 0 u
		include : union
			dispiro
				flat (l + [HSwToV sw] - [HSwToV fine]) (u - a - 2) [widths.rhs fine]
				curl (l + [HSwToV sw] - [HSwToV fine]) (u - a)
				arch.rhs u (sw -- sw) (swBefore -- fine)
				flat ([mix l r 0.5] + [HSwToV sw] / 2) (u - adb) [widths.rhs sw]
				curl ([mix l r 0.5] + [HSwToV sw] / 2) 0
			dispiro
				flat ([mix l r 0.5] + [HSwToV sw] / 2 - [HSwToV fine]) (u - a - 2) [widths.rhs fine]
				curl ([mix l r 0.5] + [HSwToV sw] / 2 - [HSwToV fine]) (u - a)
				arch.rhs u (sw -- sw) (swBefore -- fine)
				flat r (u - adb) [widths.rhs sw]
				curl r Descender

		if SLAB : begin
			include : HSerif.lt l u SideJut
			include : HSerif.lb l 0 SideJut
			include : HSerif.rb (r - [HSwToV HalfStroke]) 0 Jut

	derive-glyphs 'laoliq' 0xF16BC "mamei_coda" : function [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include [refer-glyph 'dotBelow']

