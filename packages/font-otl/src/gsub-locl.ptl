$$include './meta/macros.ptl'

import [CvDecompose LocalizedForm] from "@iosevka/glyph/relation"
import [createGsubLookupFromGr] from "./gsub-gr.mjs"

extern Set

export : define [buildLOCL gsub para glyphStore] : begin
	local rec : gsub.beginBlock

	define { chain-rule } : gsub.ChainRuleBuilder

	define latnTRK  : gsub.copyLanguage 'latn_TRK ' 'latn_DFLT'
	define latnAZE  : gsub.copyLanguage 'latn_AZE ' 'latn_DFLT'
	define latnGAG  : gsub.copyLanguage 'latn_GAG ' 'latn_DFLT'
	define latnKAZ  : gsub.copyLanguage 'latn_KAZ ' 'latn_DFLT'
	define latnTAT  : gsub.copyLanguage 'latn_TAT ' 'latn_DFLT'
	define latnCRT  : gsub.copyLanguage 'latn_CRT ' 'latn_DFLT'
	define latnVIT  : gsub.copyLanguage 'latn_VIT ' 'latn_DFLT'

	# TRK
	define loclTRK : gsub.createFeature 'locl'
	latnTRK.addFeature loclTRK
	latnAZE.addFeature loclTRK
	latnGAG.addFeature loclTRK
	latnKAZ.addFeature loclTRK
	latnTAT.addFeature loclTRK
	latnCRT.addFeature loclTRK
	loclTRK.addLookup : gsub.createLookup
		.type 'gsub_single'
		.substitutions : object
			'i' : glyphStore.ensureExists 'i.TRK'

	# VIT
	define loclVIT : latnVIT.addFeature : gsub.createFeature 'locl'

	define [sx s] : lambda [t] : t.map : lambda [x] "\(x)/\(s)"

	do "Decompose Vietnamese glyphs"

		local decompositionSubstitutions {.}
		loclVIT.addLookup : gsub.createLookup
			.type 'gsub_multiple'
			.substitutions decompositionSubstitutions

		local viCircumflex 'ẦẤẨỀẾỂỒỐỔầấẩềếểồốổ'
		local viBreve      'ẮẰẲằắẳ'
		local viShiftableMarks { 'acuteAbove' 'graveAbove' 'hookAbove' }

		foreach ch (viCircumflex + viBreve) : begin
			local u : ch.codePointAt 0
			local gn : glyphStore.queryNameByUnicode u
			local g : glyphStore.queryByNameEnsured gn
			local decomp : CvDecompose.get g
			set decompositionSubstitutions.(gn) decomp

		loclVIT.addLookup : gsub.createLookup
			.type 'gsub_chaining'
			.rules : list
				chain-rule
					{'A' 'a' 'E' 'e' 'O' 'o'} ~> nothing
					{'circumflexAbove'}       ~> nothing
					viShiftableMarks          ~> [sx 'viSide']
				chain-rule
					{'A' 'a'}                 ~> nothing
					{'breveAbove'}            ~> nothing
					viShiftableMarks          ~> [sx 'viCenter']

	# IPPH
	define loclIPPH : gsub.createFeature 'locl'
	loclIPPH.addLookup : createGsubLookupFromGr gsub glyphStore LocalizedForm.IPPH

	gsub.endBlockAtFront rec
